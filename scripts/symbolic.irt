#
# Some tests for symbolic computation.
#
#						Gershon Elber, Nov. 1992
#

#
# Set display to on to view some results, off to view nothing.
#
display = on;

#
# The symbolic computation below is faster this way.
#
IProd = iritstate( "BspProdMethod", 0 );

#
# Control the surface to polygons subdivison resolution, and isolines gen.
#
save_res = resolution;

resolution = 20;
if ( machine == msdos, (resolution = 5) );

s45 = sin( pi / 4 );

#
# marker function for curves...
#
CrvEqlParamSegMarkers = function( c ): crvsWsegs: crvsBsegs: NumSegs: i:
    crvsWsegs = nil():
    crvsBsegs = nil():
    NumSegs = 10.0:
    for ( i = 1, 1, NumSegs,
	snoc( cregion( c, ( i - 1.0 ) / NumSegs, ( i - 0.5 ) / NumSegs ),
	      crvsWsegs ): 
	snoc( cregion( c, ( i - 0.5 ) / NumSegs, ( i - 0.0 ) / NumSegs ),
	      crvsBsegs )
    ):
    color( crvsWsegs, red ):
    color( crvsBsegs, yellow ):
    return = list( crvsWsegs, crvsBsegs );

#
# Simple polynomial surface.
#
sbsp = list ( list( ctlpt( E3, 0.0, 0.0, 1.0 ),
	            ctlpt( E3, 0.0, 1.0, 0.8 ),
	            ctlpt( E3, 0.0, 2.1, 1.0 ) ),
              list( ctlpt( E3, 1.0, 0.0, 2.0 ),
	            ctlpt( E3, 1.1, 1.0, 1.0 ),
	            ctlpt( E3, 1.0, 2.1, 2.0 ) ),
              list( ctlpt( E3, 2.0, 0.0, 1.0 ),
	            ctlpt( E3, 2.0, 1.0, 0.8 ),
	            ctlpt( E3, 2.0, 2.1, 1.0 ) ),
              list( ctlpt( E3, 3.0, 0.0, 2.0 ),
	            ctlpt( E3, 3.1, 1.0, 1.8 ),
	            ctlpt( E3, 3.0, 2.1, 2.0 ) ) );
s = sbspline( 3, 4, sbsp, list( list( KV_OPEN ),
				list( KV_OPEN ) ) );
color( s, white );
free( sbsp );

dus = sderive( s, ROW ) * scale( vector( 0.5, 0.5, 0.5 ) );
color( dus, green );
dvs = sderive( s, COL ) * scale( vector( 0.5, 0.5, 0.5 ) );
color( dvs, magenta );
if ( display == on, viewobj( axes ):
		    viewstate( "DSrfMesh", 1 ):
		    interact( list( axes, s, dus, dvs ) ):
		    viewstate( "DSrfMesh", 0 ) );

ns = snrmlsrf(s) * scale( vector( 0.3, 0.3, 0.3 ) );
color( ns, green );
if ( display == on, interact( list( axes, s, ns ) ) );

# Compute the normal at the center of the surface, in three ways.

save( "nrml1srf",
      list( axes, s, ns, dus, dvs,
	    coerce( seval( dus, 0.5, 0.5 ), VECTOR_TYPE ) ^
	    coerce( seval( dvs, 0.5, 0.5 ), VECTOR_TYPE ),
	    coerce( seval( ns, 0.5, 0.5 ), VECTOR_TYPE ),
	    snormal( s, 0.5, 0.5 ) ) );

#
# A (portion of) sphere (rational surface).
#
halfcirc = cbspline( 3,
		     list( ctlpt( P3, 1.0,  0.0, 0.0,  1.0 ),
		           ctlpt( P3, s45, -s45, 0.0,  s45 ),
		           ctlpt( P3, 1.0, -1.0, 0.0,  0.0 ),
		           ctlpt( P3, s45, -s45, 0.0, -s45 ),
			   ctlpt( P3, 1.0,  0.0, 0.0, -1.0 ) ),
		     list( 0, 0, 0, 1, 1, 2, 2, 2 ) );
color( halfcirc, white );

s = surfrev( halfcirc );
color( s, white );
free( halfcirc );

dus = sderive(s, ROW);
color( dus, green );
dvs = sderive(s, COL);
color( dvs, magenta );
if ( display == on, interact( list( axes, s, dus, dvs ) ) );

ns = snrmlsrf(s);
color( ns, green );
if ( display == on, interact( list( axes, s, ns ) ) );

s = sregion( sregion( s, ROW, 0.2, 0.5 ), COL, 0.0, 2.0 );
color( s, white );

dus = sderive(s, ROW);
color( dus, green );
dvs = sderive(s, COL);
color( dvs, magenta );
if ( display == on, interact( list( axes, s, dus, dvs ) ) );

ns = snrmlsrf(s);
color( ns, green );
if ( display == on, interact( list( axes, s, ns ) ) );

#
# A Glass.
#
gcross = cbspline( 3,
		   list( ctlpt( E3, 0.3, 0.0, 0.0 ),
		         ctlpt( E3, 0.3, 0.0, 0.05 ),
		         ctlpt( E3, 0.1, 0.0, 0.05 ),
		         ctlpt( E3, 0.1, 0.0, 0.4 ),
		         ctlpt( E3, 0.5, 0.0, 0.4 ),
		         ctlpt( E3, 0.6, 0.0, 0.8 ) ),
		   list( 0, 0, 0, 1, 2, 3, 4, 4, 4 ) );
color( gcross, white );
s = surfrev( gcross );
color( s, white );
free( gcross );

dus = sderive(s, ROW);
color( dus, green );
dvs = sderive(s, COL);
color( dvs, magenta );
if ( display == on, interact( list( axes, s, dus, dvs ) ) );

ns = snrmlsrf(s);
color( ns, green );
if ( display == on, interact( list( axes, s, ns ) ) );

save( "nrml2srf", list( axes, s, ns, dus, dvs ) );


#
# Compute two surfaces, one is an offset approximation to the surface and
# the other is just a translation. Then compute the distance square scalar
# surface between them and the original surface. With the data below both
# Should have a distance square of 3 (if exact, the offset is obviously not).
#
s1 = s * trans( vector( 1, -1, 1 ) );
color( s1, green );

s2 = offset( s, sqrt( 3 ), 1.0, off );
color( s2, yellow );

DLevel = iritState( "DumpLevel", 255 );
distsqr1 = symbdprod( symbdiff( s, s1 ), symbdiff( s, s1 ) );
distsqr2 = symbdprod( symbdiff( s, s2 ), symbdiff( s, s2 ) );

DLevel = iritState( "DumpLevel", DLevel );

save( "dist1sqr", list( distsqr1, distsqr2 ) );

free( s );
free( s1 );
free( s2 );
free( distsqr1 );
free( distsqr2 );
free( dus );
free( dvs );
free( ns );

#############################################################################
#
# Curve-curve, curve-surface, and surface-surface composition.
#
DLevel = iritState( "DumpLevel", 255 );
viewstate( "DSrfMesh", 1 );

crv1 = circle( vector( 0.0, 0.0, 0.0 ), 0.8 );

crv2 = cbspline( 5,
		 list( ctlpt( E1, 0.0 ),
		       ctlpt( E1, 1.0 ),
		       ctlpt( E1, 2.0 ),
		       ctlpt( E1, 3.0 ),
		       ctlpt( E1, 4.0 ) ),
		 list( KV_OPEN ) );
crv1c = compose( crv1, crv2 );
crv1m = creparam( cmoebius( crv1, 1 ), 0, 1 );

All = list( CrvEqlParamSegMarkers( crv1c ),
	    CrvEqlParamSegMarkers( crv1m ) * tz( 0.1 ) );
if ( display == on, interact( All ) );

save("cc1comps", All );

crv2 = cbspline( 5,
		 list( ctlpt( E1, 0.0 ),
		       ctlpt( E1, 0.0 ),
		       ctlpt( E1, 0.0 ),
		       ctlpt( E1, 0.0 ),
		       ctlpt( E1, 4.0 ) ),
		 list( KV_OPEN ) );
crv1c = compose( crv1, crv2 );
crv1m = creparam( cmoebius( crv1, 0.2 ), 0, 1 );

All = list( CrvEqlParamSegMarkers( crv1c ),
	    CrvEqlParamSegMarkers( crv1m ) * tz( 0.1 ) );
if ( display == on, interact( All ) );

save("cc2comps", All );

crv2 = cbspline( 5,
		 list( ctlpt( E1, 0.0 ),
		       ctlpt( E1, 4.0 ),
		       ctlpt( E1, 4.0 ),
		       ctlpt( E1, 4.0 ),
		       ctlpt( E1, 4.0 ) ),
		 list( KV_OPEN ) );
crv1c = compose( crv1, crv2 );
crv1m = creparam( cmoebius( crv1, 5 ), 0, 1 );

All = list( CrvEqlParamSegMarkers( crv1c ),
	    CrvEqlParamSegMarkers( crv1m ) * tz( 0.1 ) );
if ( display == on, interact( All ) );

save("cc3comps", All );

################################# 

srf = sbezier( list( list( ctlpt( E3, 0.0, 0.0, 0.0 ),
		           ctlpt( E3, 0.0, 0.5, 1.0 ),
		           ctlpt( E3, 0.0, 1.0, 0.0 ) ),
	             list( ctlpt( E3, 0.5, 0.0, 1.0 ),
		           ctlpt( E3, 0.5, 0.5, 0.0 ),
		           ctlpt( E3, 0.5, 1.0, 1.0 ) ),
	             list( ctlpt( E3, 1.0, 0.0, 1.0 ),
		           ctlpt( E3, 1.0, 0.5, 0.0 ),
		           ctlpt( E3, 1.0, 1.0, 1.0 ) ) ) );
color( srf, magenta );

crv = circle( vector( 0.5, 0.5, 0.0 ), 0.4 );
color( crv, yellow );

ccrv = compose( srf, crv );
color( ccrv, cyan );

if ( display == on, interact( list( srf, crv, ccrv ) ) );

save("cs1comps", list( srf, crv, ccrv ) );

################################# 

srf = sbspline( 3, 3,
		list( list( ctlpt( E3, 0.0, 0.0, 0.0 ),
		            ctlpt( E3, 0.0, 0.5, 1.0 ),
		            ctlpt( E3, 0.0, 1.0, 0.0 ) ),
	              list( ctlpt( E3, 0.5, 0.0, 1.0 ),
		            ctlpt( E3, 0.5, 0.5, 0.0 ),
		            ctlpt( E3, 0.5, 1.0, 1.0 ) ),
	              list( ctlpt( E3, 1.0, 0.0, 1.0 ),
		            ctlpt( E3, 1.0, 0.5, 0.0 ),
		            ctlpt( E3, 1.0, 1.0, 0.9 ) ),
	              list( ctlpt( E3, 1.5, 0.0, 0.0 ),
		            ctlpt( E3, 1.5, 0.5, 1.1 ),
		            ctlpt( E3, 1.5, 1.0, 0.0 ) ),
	              list( ctlpt( E3, 2.1, 0.0, 1.0 ),
		            ctlpt( E3, 2.0, 0.5, 0.5 ),
		            ctlpt( E3, 2.1, 1.0, 1.1 ) ) ),
		list( list( kv_open ), list( kv_open ) ) );
color( srf, magenta );

crv = circle( vector( 0.5, 0.5, 0.0 ), 0.4 );
color( crv, yellow );

ccrv = compose( srf, crv );
color( ccrv, cyan );

if ( display == on, interact( list( srf, crv, ccrv ) ) );

save("cs2comps", list( srf, crv, ccrv ) );

################################# 

srf = sreparam( surfPRev( ctlpt( E3, 1, 0, -1 ) + ctlpt( E3, 1, 0, 1 ) ),
		col, 0, 1 ) * sc( 0.5 );
color( srf, magenta );

crv = pcircle( vector( 0.45, 0.55, 0.0 ), 0.4 );
color( crv, yellow );

ccrv = compose( srf, crv );
color( ccrv, cyan );

if ( display == on, interact( list( axes, srf, crv, ccrv ) ) );

save("cs3comps", list( srf, crv, ccrv ) );

for ( x = -1.1, 0.02, 0.9,
    crv = pcircle( vector( x, 0.45, 0.0 ), 0.4 ):
    color( crv, yellow ):

    ccrv = compose( srf, crv ):
    color( ccrv, cyan ):

    if ( display == on, view( list( axes, srf, crv, ccrv ), 1 ) ) );
free( x );

for ( y = -1.1, 0.02, 0.9,
    crv = pcircle( vector( 0.45, y, 0.0 ), 0.4 ):
    color( crv, yellow ):

    ccrv = compose( sreverse( srf ), crv ):
    color( ccrv, cyan ):

    if ( display == on, view( list( axes, srf, crv, ccrv ), 1 ) ) );
free( y );

interact( list( axes, srf, crv, ccrv ) );

save("cs4comps", list( srf, crv, ccrv ) );



srf = sbezier( list( list( ctlpt( E3, 0.0, 0.0, 0.0 ),
		           ctlpt( E3, 0.0, 0.5, 1.0 ),
		           ctlpt( E3, 0.0, 1.0, 0.0 ) ),
	             list( ctlpt( E3, 0.5, 0.0, 1.0 ),
		           ctlpt( E3, 0.5, 0.5, 0.0 ),
		           ctlpt( E3, 0.5, 1.0, 1.0 ) ),
	             list( ctlpt( E3, 1.0, 0.0, 1.0 ),
		           ctlpt( E3, 1.0, 0.5, 0.0 ),
		           ctlpt( E3, 1.0, 1.0, 1.0 ) ) ) );
color( srf, magenta );

#################################

srf2 = ruledSrf( ctlpt( E2, 0.1, 0.2 ) + ctlpt( E2, 0.2, 0.5 ),
		 ctlpt( E2, 0.5, 0.2 ) + ctlpt( E2, 0.8, 0.7 ) );
color( srf2, blue );

scomp = compose( srf, srf2 );
color( scomp, cyan );

interact( list( axes, srf, srf2, scomp ) );

save("ss5comps", list( srf, srf2, scomp ) );

srf2 = sbspline( 3, 3,
		list( list( ctlpt( E2, 0.1, 0.1 ),
		            ctlpt( E2, 0.2, 0.4 ),
		            ctlpt( E2, 0.1, 0.6 ) ),
	              list( ctlpt( E2, 0.3, 0.1 ),
		            ctlpt( E2, 0.4, 0.2 ),
		            ctlpt( E2, 0.2, 0.5 ) ),
	              list( ctlpt( E2, 0.5, 0.2 ),
		            ctlpt( E2, 0.5, 0.4 ),
		            ctlpt( E2, 0.7, 0.7 ) ),
	              list( ctlpt( E2, 0.6, 0.2 ),
		            ctlpt( E2, 0.7, 0.5 ),
		            ctlpt( E2, 0.9, 0.6 ) ) ),
		list( list( kv_open ), list( kv_open ) ) );
color( srf2, blue );

scomp = compose( srf, srf2 );
color( scomp, cyan );

interact( list( axes, srf, srf2, scomp ) );

save("ss6comps", list( srf, srf2, scomp ) );

#################################

crv = cbezier( list( ctlpt( E2, 0.2, 0.0 ),
		     ctlpt( E2, 0.8, 0.5 ),
		     ctlpt( E2, 0.2, 1.0 ) ) );

srfs = sdivcrv( srf, crv );

interact( list( axes, srf, srfs ) );

save("sdivcrv1", list( srf, srfs ) );

crv = cbspline( 3,
	        list( ctlpt( E2, 0.0, 0.2 ),
		      ctlpt( E2, 0.2, 0.8 ),
		      ctlpt( E2, 0.4, 0.2 ),
		      ctlpt( E2, 0.6, 0.8 ),
		      ctlpt( E2, 0.8, 0.2 ),
		      ctlpt( E2, 1.0, 0.8 ) ),
		list( kv_open ) );
srfs = sdivcrv( srf, crv );

interact( list( axes, srf, srfs ) );

save("sdivcrv2", list( srf, srfs ) );

#############################################################################
#
# Curve-trivar and surface-trivar composition.
#

tv1 = tbezier( list( list( list( ctlpt( P3, 1.0, 0.1, 0.0, 0.8 ),
				 ctlpt( P3, 0.9, 0.2, 0.1, 2.4 ) ),
			   list( ctlpt( P3, 1.0, 0.3, 2.2, 0.2 ),
				 ctlpt( P3, 1.0, 0.4, 2.3, 2.0 ) ) ),
		     list( list( ctlpt( P3, 1.0, 1.4, 0.8, 0.1 ),
				 ctlpt( P3, 1.0, 1.2, 0.7, 2.3 ) ),
			   list( ctlpt( P3, 1.3, 1.3, 2.6, 0.5 ),
				 ctlpt( P3, 1.2, 1.1, 2.5, 4.7 ) ) ),
		     list( list( ctlpt( P3, 1.0, 2.4, 0.8, 0.1 ),
				 ctlpt( P3, 1.0, 2.2, 0.7, 2.3 ) ),
			   list( ctlpt( P3, 0.7, 2.3, 2.6, 0.5 ),
				 ctlpt( P3, 1.0, 2.1, 2.5, 2.7 ) ) ) ) )
	* sc( 0.3 );
color( tv1, yellow );
attrib( tv1, "transp", 0.5 );

sv2 = sbezier( list( list( ctlpt( E3, 0.1, 0.1, 0.0 ),
			   ctlpt( E3, 0.2, 0.5, 1.1 ),
			   ctlpt( E3, 0.3, 0.1, 2.8 ) ),
		     list( ctlpt( E3, 0.4, 1.3, 0.5 ),
		 	   ctlpt( E3, 1.5, 1.7, 3.7 ),
			   ctlpt( E3, 0.6, 2.3, 4.9 ) ),
		     list( ctlpt( E3, 0.7, 2.4, 1.5 ),
			   ctlpt( E3, 0.8, 2.6, 2.4 ),
			   ctlpt( E3, 0.9, 2.8, 3.3 ) ) ) );
tv2 = tFromSrfs( list( sv2 * tx( -1 ),
		       sv2 * sc( 0.1 ) * tx( 0 ),
		       sv2 * sc( 2 ) * tx( 1 ),
		       sv2 * sc( 1 ) * tx( 2 ) ),
		 4, kv_open ) * sc( 0.3 ) * tx( -1 );
free( sv2 );

color( tv2, yellow );
attrib( tv2, "transp", 0.5 );

#################################

arc1 = cbezier( list( ctlpt( E3, 0.0, 0.0, 0.0 ),
	   	      ctlpt( P3, 1.5, 0.8, 1.5, 0.0 ),
	  	      ctlpt( P3, 1.5, 1.5, 0.0, 1.5 ),
	  	      ctlpt( E3, 1.0, 1.0, 1.0 ) ) );
	     
Hlx = chelix( 3, 0.0333, 0.44, 100, 6 ) * tx( 0.55 ) * ty( 0.6 );

cc1 = compose( tv1, arc1 );
color( cc1, green );

cc2 = compose( tv1, Hlx );
color( cc2, cyan );

cc3 = compose( tv2, arc1 );
color( cc3, green );

cc4 = compose( tv2, Hlx );
color( cc4, cyan );

free( arc1 );
free( Hlx );

All = list( cc1, cc2, cc3, cc4, tv1, tv2 );

interact( All );

save( "ct1comps", All );

free( cc1 );
free( cc2 );
free( cc3 );
free( cc4 );

#################################
#
# Trivar - surface composition
#

srf1 = sbspline( 2, 4,
		 list( list( ctlpt( E3, 0.0, 0.0, 1.0 ),
		             ctlpt( E2, 0.0, 1.0 ),
		             ctlpt( E3, 0.0, 2.0, 1.0 ) ),
        	       list( ctlpt( E2, 1.0, 0.0 ),
		             ctlpt( E3, 1.0, 1.0, 2.0 ),
		             ctlpt( E2, 1.0, 2.0 ) ),
        	       list( ctlpt( E3, 2.0, 0.0, 2.0 ),
	        	     ctlpt( E2, 2.0, 1.0 ),
		             ctlpt( E3, 2.0, 2.0, 2.0 ) ),
        	       list( ctlpt( E2, 3.0, 0.0 ),
	        	     ctlpt( E3, 3.0, 1.0, 2.0 ),
		             ctlpt( E2, 3.0, 2.0 ) ),
        	       list( ctlpt( E3, 4.0, 0.0, 1.0 ),
	        	     ctlpt( E2, 4.0, 1.0 ),
		             ctlpt( E3, 4.0, 2.0, 1.0 ) ) ),
		 list( list( KV_OPEN ),
		       list( KV_OPEN ) ) ) * sx( 0.5 ) * sc( 0.5 ) * tz( 0.2 );

Srf2 = cylinSrf( 0.9, 0.4 ) * tx( 0.5 ) * ty( 0.5 ) * tz( 0.05 );

sc1 = compose( tv1, srf1 );
color( sc1, green );

sc2 = compose( tv1, srf2 );
color( sc2, cyan );

sc3 = compose( tv2, srf1 );
color( sc3, green );

sc4 = compose( tv2, srf2 );
color( sc4, cyan );

All = list( sc1, sc2, sc3, sc4, tv1, tv2 );

interact( All );

save( "st1comps", All );

free( sc1 );
free( sc2 );
free( sc3 );
free( sc4 );

free( tv1 );
free( tv2 );

#############################################################################
#
# Bspline basis inner products
#

i = symbiprod( crv = pcircle( vector( 0, 0, 0 ), 1 ), 4, 4 );
for ( i = 0, 1, nth( ffmsize( crv ), 1 ) - 1,
    for ( j = 0, 1, nth( ffmsize( crv ), 1 ) - 2,
        printf( "%3.3f ", list( symbiprod( 0, i, j ) ) ) ):
    printf( "\\n", nil() ) );

#############################################################################

resolution = save_res;

free( display );
free( s45 );
free( srf );
free( srf1 );
free( srf2 );
free( srfs );
free( scomp );
free( crv );
free( crv1 );
free( crv1c );
free( crv1m );
free( crv2 );
free( ccrv );
free( All );
free( i );
free( j );

DLevel = iritState( "DumpLevel", DLevel );
free( DLevel );

IProd = iritstate( "BspProdMethod", IProd );
free( IProd );
