#############################################################################
#
# Warping teapot using FFD trivariates.
#
#						Gershon Elber, Sep 1999.
#

save_mat2 = view_mat;
view_mat = rotx( 0 );

#
# Get a model of a teapot.
#

EchoSrc2 = iritState( "EchoSource", false );
interact = procedure(none):
    viewclear();

include("teapot");
TeapotOrig = load( "teapot" );

interact = procedure(none):
    viewdclear():
    viewobj(none):
    pause();
EchoSrc2 = iritState( "EchoSource", EchoSrc2 );
free( EchoSrc2 );

#
# Define the trivarate warping function.
#

s1 = ruledSrf( ctlpt( E3, 0.5, 0.0, 0.0 ) + ctlpt( E3, 1.5, 0.0, 0.0 ),
	       ctlpt( E3, 0.5, 1.2, 0.0 ) + ctlpt( E3, 1.5, 1.2, 0.0 ) );
Tv = TFromSrfs( list( s1,
		      s1 * ry( 30 ),
		      s1 * ry( 60 ),
		      s1 * ry( 90 ),
		      s1 * ry( 120 ),
		      s1 * ry( 150 ),
		      s1 * ry( 180 ) ),
		3, kv_open );
Tv = treparam( treparam( Tv, col, 0.0, 0.5 ), row, 0.2, 0.8 );
awidth( Tv, 0.001 );
free( s1 );

#
# Define our warping function.
#

WarpSurface1 = function( Srf, Tv ): i: Clr: USize: VSize: Pt: x: y: z: u: v:
    USize = nth( ffmsize( Srf ), 1 ):
    VSize = nth( ffmsize( Srf ), 2 ):
    Clr = getAttr( Srf, "color" ):
    for ( i = 0, 1, USize * VSize - 1,
	Pt = coord( Srf, i ):
	x = coord( Pt, 1 ):
	y = coord( Pt, 2 ):
	z = coord( Pt, 3 ):
	Pt = teval( Tv, x, y, z ):
	v = floor( i / USize ):
	u = i - v * USize:
	Srf = sEditPt( Srf, Pt, u, v ) ):
    attrib( Srf, "color", Clr ):
    return = Srf;

WarpSurface2 = function( Srf, x, y, z, Tv, Precise ): Clr:
    Clr = getAttr( Srf, "color" ):
    return = tdeform( Srf, Tv, x, y, z, false, Precise ):
    attrib( return, "color", Clr );

#
# Properly orient the Teapot in the parametric space of the Trivariate
#
PrmDomain = box( vector( 0, 0, 0 ), 0.5, 1, 1 );
attrib( PrmDomain, "transp", 0.8 );

Teapot = TeapotOrig * sc( 0.13 ) * rz( 90 ) * rx( 90 ) * sx( -1 )
	* trans( vector( 0.0, 0.5, 0.5 ) );

All = list( PrmDomain, Teapot ) * rz( 90 ) * ry( 40 ) * rx( 40 );

interact( list( All, view_mat ) );
save( "warp1trv", All );

#
# Warp1 the teapot, one surface at a time, after some surface refinement.
#
WarpedTeapot1 = nil();
for ( i = 1, 1, sizeof( Teapot ),
    Srf = nth( Teapot, i ):
    Clr = getAttr( Srf, "color" ):
    Srf = sreparam( sreparam( Srf, col, 0, 1 ), row, 0, 1 ):
    Srf = srefine( srefine( Srf, col, false, list( 0.1, 0.2, 0.3, 0.4, 0.5,
					           0.6, 0.7, 0.8, 0.9 ) ),
		   row, false, list( 0.1, 0.2, 0.3, 0.4, 0.5,
				     0.6, 0.7, 0.8, 0.9 ) ):
    attrib( Srf, "color", Clr ):
    snoc( WarpSurface1( Srf, Tv ), WarpedTeapot1 ) );

PrmDomain1 = list( sTrivar( Tv, col, 0.0 ),
		   sTrivar( Tv, col, 0.5 ),
		   sTrivar( Tv, row, 0.2 ),
		   sTrivar( Tv, row, 0.8 ),
		   sTrivar( Tv, depth, 0.0 ),
		   sTrivar( Tv, depth, 1.0 ) );
attrib( PrmDomain1, "transp", 0.8 );

All1 = list( WarpedTeapot1, PrmDomain1 );

#
# Warp2 the teapot, one surface at a time, after some surface refinement.
#
WarpedTeapot2 = nil();
for ( i = 1, 1, sizeof( Teapot ),
    Srf = nth( Teapot, i ) * sx( 2 ) 
			   * sy( 1.75 ) * ty( -0.35 )
		           * tz( -0.1 ) * sz( 1.2 ):
    Clr = getAttr( Srf, "color" ):
    Srf = sreparam( sreparam( Srf, col, 0, 1 ), row, 0, 1 ):
    attrib( Srf, "color", Clr ):
    snoc( WarpSurface2( Srf, 2, 1, 5, Tv, false ), WarpedTeapot2 ) );

PrmDomain2 = list( sTrivar( Tv, col, 0.0 ),
	 	   sTrivar( Tv, col, 0.5 ),
	 	   sTrivar( Tv, row, 0.2 ),
	 	   sTrivar( Tv, row, 0.8 ),
	 	   sTrivar( Tv, depth, 0.0 ),
		   sTrivar( Tv, depth, 1.0 ) );
attrib( PrmDomain2, "transp", 0.8 );

All2 = list( WarpedTeapot2, PrmDomain2 );

#
# Warp2 the teapot, one surface at a time, after some surface refinement.
#
WarpedTeapot3 = nil();
for ( i = 1, 1, sizeof( Teapot ),
    Srf = nth( Teapot, i ) * sx( 2 ) 
			   * sy( 1.75 ) * ty( -0.35 )
		           * tz( -0.1 ) * sz( 1.2 ):
    Clr = getAttr( Srf, "color" ):
    Srf = sreparam( sreparam( Srf, col, 0, 1 ), row, 0, 1 ):
    attrib( Srf, "color", Clr ):
    snoc( WarpSurface2( Srf, 2, 1, 1, Tv, true ), WarpedTeapot3 ) );

PrmDomain3 = list( sTrivar( Tv, col, 0.0 ),
	 	   sTrivar( Tv, col, 0.5 ),
	 	   sTrivar( Tv, row, 0.2 ),
	 	   sTrivar( Tv, row, 0.8 ),
	 	   sTrivar( Tv, depth, 0.0 ),
		   sTrivar( Tv, depth, 1.0 ) );
attrib( PrmDomain3, "transp", 0.8 );

All3 = list( WarpedTeapot3, PrmDomain3 );

All = list( All1 * ty( -2 ),
	    All2 * ty( 0 ),
	    All3 * ty( 2 ) ) * rx( 90 ) * ry( 20 ) * rx( 10 ) * sc( 0.5 );

interact( list( All, view_mat ) );

save( "warp2trv", All );

#############################################################################
#
# Let the Genie come out of the teapot...
#

Teapot = TeapotOrig * sc( 0.2 ) * sx( -1 ) * rx( 90 ) * rz( 180 );

s = planeSrf( -1, -1, 1, 1 ) * sc( 1.7 );
Discs = list( s * sc( 0.01 ) * sx( 2 ) * tx( 0.58 ) * tz( 0.42 ),
	      s * sc( 0.01 ) * sx( 2 ) * tx( 0.62 ) * tz( 0.46 ),
	      s * sc( 0.05 ) * sx( 1.5 ) * tx( 0.65 ) * tz( 0.55 ),
	      s * sc( 0.07 ) * sx( 1.5 ) * tx( 0.7 ) * tz( 0.7 ),
	      s * sc( 0.09 ) * sx( 1.5 ) * tx( 0.65 ) * tz( 0.85 ),
	      s * sc( 0.08 ) * sx( 1.5 ) * tx( 0.7 ) * tz( 1 ),
	      s * sc( 0.07 ) * tx( 0.7 ) * tz( 1.1 ) );
Tv = tFromSrfs( Discs, 3, kv_open );

# Create a refined cylinder to warp out of the teapot...

c = creparam( pcircle( vector( 0.5, 0.5, 0.001 ), 0.45 ), 0, 1 );
Srf = extrude( c, vector( 0, 0, 0.99 ), 0 );
Srf = srefine( srefine( Srf, col, false, list( 0.1, 0.2, 0.3, 0.4,
					       0.6, 0.7, 0.8, 0.9 ) ),
		        row, false, list( 0.1, 0.2, 0.3, 0.4, 0.5,
				          0.6, 0.7, 0.8, 0.9 ) );
attrib( Srf, "color", yellow ):
WarpedSrf = WarpSurface1( -Srf, Tv );
attrib( WarpedSrf, "ptexture", "g.gif" );

interact( list( Teapot, WarpedSrf ) );

All = list( Teapot, WarpedSrf );

save( "warp3trv", All );

#############################################################################
#
# A trimmed surfaces tile example.
#

Rad1 = 0.2;
Rad2 = 0.3;
Rad3 = 0.2999999;

GenCylinModel = function( Rad ):
   return = surfPRev( ctlpt( E3, -Rad, 0, -1 ) + ctlpt( E3, -Rad, 0, 1 ) ):
   return = coerce( return, model_type );

Cylin1 = GenCylinModel( Rad1 );
Cylin2 = GenCylinModel( Rad2 ) * rx( 90 );
Cylin3 = GenCylinModel( Rad3 ) * ry( 90 );


Cylin23 = Cylin2 + Cylin3;

Cylin123 = Cylin1 + Cylin23;

TrimCylin123 = coerce( Cylin123, trimsrf_type )
			 * sc( 0.5 ) * tx( 0.5 ) * ty( 0.5 ) * tz( 0.5 );

interact( TrimCylin123 );

free( Rad1 );
free( Rad2 );
free( Rad3 );
free( Cylin1 );
free( Cylin2 );
free( Cylin3 );
free( Cylin23 );
free( Cylin123 );

TVCylin123 = TDeform( TrimCylin123, TV, 2, 2, 2, FALSE, TRUE );
color( TVCylin123, red );

attrib( TV, "transp", 0.8 );

All = list( TVCylin123, TV );

interact( list( axes, All ) );

save( "warp4trv", All );

#############################################################################

view_mat = save_mat2;

free( TrimCylin123 );
free( TVCylin123 );
free( TeapotOrig );
free( Teapot );
free( WarpedTeapot1 );
free( WarpedTeapot2 );
free( WarpedTeapot3 );
free( Clr );
free( WarpedSrf );
free( Srf );
free( All );
free( All1 );
free( All2 );
free( All3 );
free( Discs );
free( i );
free( c );
free( s );
free( Tv );
free( PrmDomain );
free( PrmDomain1 );
free( PrmDomain2 );
free( PrmDomain3 );
free( Body );
free( Spout );
free( Handle );
free( Cap );
