#
# Few examples of text warping through surfaces using the textwarp function.
#
#						Gershon Elber, Jan 2003.
#

c1 = cbezier( list( ctlpt( e2, -2,  0 ),
		    ctlpt( e2, -1,  1 ),
		    ctlpt( e2,  0, -1 ),
		    ctlpt( e2,  1,  0 ) ) );
c2 = cbezier( list( ctlpt( e1, -0.0 ),
		    ctlpt( e1, -1.8 ),
		    ctlpt( e1, -0.0 ) ) );
s1 = sreparam( ruledSrf( c1, offset( c1, -0.4, 0.02, off ) ), col, 0, 6 );
color( s1, red );
t1 = textwarp( s1, "Computer Graphics", 0.09, 0.25, 0.75, 0 );
color( t1, cyan );
adwidth( t1, 2 );
interact( list( t1, s1 ) );

t1 = textwarp( s1, "Computer Graphics", 0.1, 0.25, 0.75, 1 );
color( t1, cyan );
adwidth( t1, 2 );

interact( list( t1, s1 ) );

s2 = sreparam( ruledSrf( c1, offset( c1, c2, 0.01, off ) ), col, 0, 6 );
color( s2, red );
t2 = textwarp( s2, "Computer Graphics", 0.15, 0.25, 0.75, 0.55 );
color( t2, cyan );
adwidth( t2, 2 );

interact( list( t2, s2 ) );


s3 = sreparam( sphereSrf( 1 ), col, 0, 6.5 );
color( s3, red );
t3 = textwarp( s3, "A Sphere", 0.1, 0.2, 0.9, 0.85 );
color( t3, cyan );
adwidth( t3, 2 );

interact( list( t3, s3 ) );

save( "txt1warp", list( list( s1, t1 ) * ty( 1 ),
		        list( s2, t2 ),
		        list( s3, t3 ) * rx( -90 ) * ty( -2 ) ) );

#################################

if ( machine == windows, # Use build in outline fonts.
    txt = Text2Geom( "Computer Graphics", "Times New Roman",
		     0, 0, 2, list( 0.012, 0.1 ), 0.001, 1 )
				* sx( 0.067 ) * sy( 0.5 ) * ty( 0.25 ),
    # Try freetype.  Font is actual ttf file name in the file system
    txt = Text2Geom( "Computer Graphics",
		     "/home/gershon/bin/S721-Script-One-Regular.ttf
",
		     0, 0, 0, list( 0.012, 0.1 ), 0.001, 1 )
				* sx( 0.35 ) * sy( 2 ) * ty( 0.25 ) );

t1 = nil();
for ( i = 1, 1, sizeof( txt ),
    ch = nth( txt, i ):
    if ( thisobj("ch") == list_type,
	for ( j = 1, 1, sizeof( ch ),
	    snoc( TDeform( coord( ch, j ), s1, 1, 1, 1, FALSE, TRUE ), t1 ) ),
	snoc( TDeform( ch, s1, 1, 1, 1, FALSE, TRUE ), t1 ) ) );
adwidth( t1, 2 );

interact( list( t1, s1 ) );

t2 = nil();
for ( i = 1, 1, sizeof( txt ),
    ch = nth( txt, i ):
    if ( thisobj("ch") == list_type,
	for ( j = 1, 1, sizeof( ch ),
	    snoc( TDeform( coord( ch, j ), s2, 1, 1, 1, FALSE, TRUE ), t2 ) ),
	snoc( TDeform( ch, s2, 1, 1, 1, FALSE, TRUE ), t2 ) ) );
adwidth( t2, 2 );

interact( list( t2, s2 ) );

t3 = nil();
for ( i = 1, 1, sizeof( txt ),
    ch = nth( txt, i ):
    if ( thisobj("ch") == list_type,
	for ( j = 1, 1, sizeof( ch ),
	    snoc( TDeform( coord( ch, j ), s3, 1, 1, 1, FALSE, TRUE ), t3 ) ),
	snoc( TDeform( ch, s3, 1, 1, 1, FALSE, TRUE ), t3 ) ) );
adwidth( t3, 2 );

interact( list( s3, t3 ) );

save( "txt2warp", list( list( s1, t1 ) * ty( 1 ),
		        list( s2, t2 ),
		        list( s3, t3 ) * rx( -90 ) * ty( -2 ) ) );

#################################

free( i );
free( j );
free( txt );
free( c1 );
free( c2 );
free( s1 );
free( s2 );
free( s3 );
free( t1 );
free( t2 );
free( t3 );
free( ch );
